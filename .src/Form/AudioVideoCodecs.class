' Gambas class file

'Copyright (C) 2007, 2008 Antonio Orefice
' Gambas class file

Private mplayer As New MplayerClass
Public AlreadyLoaded As Boolean = False 'true if the form loaded it's settings from disk at least one time

Public Sub ACombos_Enter()
  Dim PrevText As String = Last.text
  If Last.List.count < 2 Then
    Last.List = mplayer.CodecsList("AUDIO").sort(gb.IgnoreCase)
    Last.List.sort
    Last.text = PrevText
  Endif
End

Public Sub VCombos_Enter()
  Dim PrevText As String = Last.text
  If Last.List.count < 2 Then
    Last.List = mplayer.CodecsList("VIDEO").sort(gb.IgnoreCase)
    Last.text = PrevText
  Endif
End

Public Sub DCombos_Enter()
  Dim PrevText As String = Last.text
  If Last.List.count < 2 Then
    Last.List = mplayer.DeMuxerslist().sort(gb.IgnoreCase)
    Last.text = PrevText
  Endif
End


Public Sub FirstLoadIfNeeded()
  If Not AlreadyLoaded Then 
     MenuLoadDefault_Click()
     Alreadyloaded = True
  Endif
End



Public Sub Form_Open()
  FirstLoadIfNeeded()
  Global.Center(Fmain, Me)
End

Public Sub MenuLoadDefault_Click()
  Dim MySettings As New SettingsClass
  MySettings.Load(Me, global.confpath & "/" & global.CurrentProfile)
  Alreadyloaded = True
End

Public Sub MenuSaveDefault_Click()
  SaveCurrent()
End
Public Sub SaveBTN_Click()
  SaveCurrent()
  
End
Public Sub SaveCurrent()
  Dim MySettings As New SettingsClass
  MySettings.Save(Me, global.confpath & "/" & global.CurrentProfile)
End

Public Sub MenuSaveToAll_Click()
  Dim profilename, profilenamesplitted As String
  Dim MySettings As New SettingsClass
  If MyQuestion.Ask("This will save this window settings\n to all profiles found but the 'Factory_defaults' one", "Proceed", "Don't") = 2 Then
    Return
      Else
     For Each profilename In Dir(global.confpath & "/", "*.profile")
        If Stat(global.confpath & "/" & profilename, True).type = gb.Directory Then
          profilenamesplitted = Split(profilename, ".")[0]
          If profilenamesplitted <> "Factory_Defaults" Then
            MySettings.Save(Me, global.confpath & "/" & profilenamesplitted)
          Endif
        Endif
     Next 'profilename
  Endif
End

Public Sub ApplyBTN_Click()
    FMain.Apply()
End

Public Sub OkBTN_Click()
  Me.hide
  Wait 0.1
  FMain.VideoBox.SetFocus()
End

Public Function Parse() As String
'returns something like "-vc codec1,codec2 -ac codec1,codec2,"
'or -vc, -ac,
  Dim Acodecs As String = "-ac "
  Dim Vcodecs As String = "-vc "
  Dim Demuxers As String
  Dim Mycombobox As Object
  Dim VdpauCodecs As String = "ffmpeg12vdpau,ffh264vdpau,ffwmv3vdpau,ffvc1vdpau,"
  
  If FMain.fullyloaded Then
    'parse Demuxers combobox and build commandline
    Try Demuxers = Split(ACombobox7.text, " ", "", True)[0]
    'do the user wants to force the demuxer?
    If dcheckbox1.value Then Demuxers = "+" & Demuxers
    demuxers = "-demuxer " & demuxers

    'if no demuxer specified, remove the whole option
    If Trim(demuxers) = "-demuxer" Then Demuxers = ""

    'parse Audio codecs comboboxes and build commandline
    For Each MyCombobox In audiolist.Children
      If (mycombobox Is ComboBox) And (Trim(mycombobox.text) <> "") Then
        Acodecs = Acodecs & mycombobox.text & ","
      Endif
    Next

    'remove the last "," if the user want
    If Not (acheckbox1.value) Then Acodecs = Left(acodecs, Len(acodecs) - 1)
    'if no codec specified, remove the whole option
    If Trim(Acodecs) = "-ac" Then Acodecs = ""
  
    'parse Video codecs comboboxes and build commandline
    If (XConfigureVideoDriver.vdpauBTN2.value = True) And (XConfigureVideoDriver.AddVdpauCHK.value = True) Then Vcodecs = Vcodecs & VdpauCodecs

    For Each MyCombobox In videolist.Children
      If (mycombobox Is ComboBox) And (Trim(mycombobox.text) <> "") Then
        Vcodecs = Vcodecs & mycombobox.text & ","
      Endif
    Next
    'remove the last "," if the user don't want to try other codecs.
    If Not (vcheckbox1.value) Then Vcodecs = Left(Vcodecs, Len(Vcodecs) - 1)
    'if no codec specified, remove the whole option
    If Trim(Vcodecs) = "-vc" Then Vcodecs = ""

    Return Acodecs & " " & Vcodecs & " " & demuxers
  Endif
End

Public Sub Form_Show()
  Shell FMain.mplayer.executable() & " -noconfig all -ac help" To Audiotext.Text
  Shell FMain.mplayer.executable() & " -noconfig all -vc help" To Videotext.Text
End



Public Sub Form_Hide()
  FMain.VideoBox.setfocus()
End

Public Sub Form_Close()
  FMain.VideoBox.setfocus()
End


Public Sub ACombos_Change()
  global.NotifyApplyNeeds()
End

Public Sub VCombos_Change()
  global.NotifyApplyNeeds()
End

Public Sub ACheckBox1_Click()
  global.NotifyApplyNeeds()
End

Public Sub VCheckBox1_Click()
  global.NotifyApplyNeeds()
End

Public Sub DCombos_Change()
  global.NotifyApplyNeeds()
End

Public Sub DCheckBox1_Click()
  global.NotifyApplyNeeds()
End
